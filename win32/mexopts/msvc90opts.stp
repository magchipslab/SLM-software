#  $Revision: 1.1.6.4.6.1 $
#  Copyright 2007-2011 The MathWorks, Inc.

sub msvc90opts 
{
    my $input = shift;
    my $found_sdk = 0;
    my $default_location = "C:\\Program Files\\Microsoft Visual Studio 9.0";
    my $sdk_dir = "";
    my $registry_lookup_fcn = $input->{"registry_lookup"};

    # Attempt to find the SDK directory.

    if ($ENV{'WINDOWSSDKDIR'} ne "")
    {
        $sdk_dir=$ENV{'WINDOWSSDKDIR'};
        $found_sdk=1;
    }
    if ($found_sdk ne 1)
    {
        my $lookedupval =  &$registry_lookup_fcn("SOFTWARE\\Microsoft\\Microsoft SDKs\\Windows\\" , "CurrentInstallFolder");
        if ($lookedupval ne "")
        {
             $sdk_dir = $lookedupval;
             $found_sdk=1;   
        }
    }

    my @language_handled = ('C');

    my $hasRequiredFiles = sub {
        my $rootDir = $_[0];
        my $isValid = 0;
        if ( $rootDir ne "" &&
             -e "$rootDir\\VC\\bin\\cl.exe" &&
             -e "$rootDir\\Common7\\IDE\\devenv.exe" ) { 
                my $oldPath = $ENV{'PATH'};
                $ENV{'PATH'} = "$rootDir\\Common7\\IDE;$oldPath";
                if (&correct_version("$rootDir\\VC\\bin\\cl.exe","15.0")) { 
                    $isValid = 1; 
                }
                $ENV{'PATH'} = "$oldPath";
        }
        return $isValid;
    };

    my $locate_fcn = sub {
        my @valid_locations = ();
        my $candidate_location;

        # If we didn't find the SDK directory before, don't bother looking
        # for the compiler

        if ($found_sdk ne 1)
        {
            return @valid_locations;
        } 

        # Search the default environment variable location. 
        # Since MSVC 2008 Professional ed. and the Express ed.
        # use the same environment variable (VS90COMNTOOLS),
        # use the directory structure to differentiate between the two.
        $candidate_location = $ENV{'VS90COMNTOOLS'};
        $candidate_location =~ s|\\+Common7\\+Tools\\*$||i;
        if ( &$hasRequiredFiles($candidate_location) )
        {
             push(@valid_locations, $candidate_location);
        }

        # Search the registry. 
        $candidate_location = &$registry_lookup_fcn("SOFTWARE\\Microsoft\\VisualStudio\\9.0\\" . 
                                            "Setup\\VC", "ProductDir");
        $candidate_location =~ s|\\VC\\$||i; 
        if ( &$hasRequiredFiles($candidate_location) )
        { 
             push(@valid_locations, $candidate_location);
        }

        # Search the default install location. 
        $candidate_location = $default_location;
        if ( &$hasRequiredFiles($candidate_location) )
        { 
             push(@valid_locations, $candidate_location);
        }
        
        return @valid_locations;
    };

    my $root_val = sub {
        my $base_directory = shift;

        print "\n***************************************************************************\n" .
              "  Warning: MEX-files generated using Microsoft Visual C++ 2008 require\n" .
              "           that Microsoft Visual Studio 2008 run-time libraries be \n" .
              "           available on the computer they are run on.\n" .
              "           If you plan to redistribute your MEX-files to other MATLAB\n" .
              "           users, be sure that they have the run-time libraries.\n" . 
              "***************************************************************************\n";

        return $base_directory;
    };

# In the case of the VS2008 compiler the linker directory is used to specify
# the location of the SDK

    return {
        "vendor_name"      => "Microsoft Visual C++ 2008 SP1",
        "version"          => "",                          #This version is left blank intentionally. 
        "group_id"         => "MSVC",
        "serial"           => 9.0,
        "root_var"         => "VSINSTALLDIR",
        "optfile_name"     => "msvc90opts.bat",
        "default_location" => $default_location,
        "language_handled" => \@language_handled,
        "root_val"         => $root_val,
        "link_val"         => $sdk_dir,
        "locate"           => $locate_fcn,
        };
}
1;
